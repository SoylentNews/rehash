#!/usr/bin/perl -w
# This code is a part of Slash, and is released under the GPL.
# Copyright 1997-2003 by Open Source Development Network. See README
# and COPYING for more information, or see http://slashcode.com/.
# $Id$

###############################################################################
# portald  - this is the "daemon" responsible for retrieving portal and site
# block content
###############################################################################

=head1 Welcome to Portald

portald is the script that sucks down headlines from assorted
places on the internet, and puts them in the boxes for use on
Slashdot.  Exciting?  Nope.

=cut

use strict;
# this needs to be a global here so we can add to it from externally
# called scripts (see portald-site)
use vars qw(%savedBlocks);
use File::Spec::Functions;
use LWP::UserAgent;
use HTTP::Request;
use URI::Escape;
use XML::RSS;

use Slash;
use Slash::Utility;
use Slash::Display;

my $virtual_user = $ARGV[0];
createEnvironment($ARGV[0]);
my $constants = getCurrentStatic();
my $slashdb = getCurrentDB();
my $totalChangedStories = 1;

my $backupdb = getObject('Slash::DB', { db_type => 'reader' });

################################################################################
# really tired of deleting the thing if portald pukes ;)
END {
    doLogExit('portald');
}

################################################################################
sub geturl {
	my($url, $options) = @_;
	my $ua = new LWP::UserAgent;
	my $request = new HTTP::Request('GET', $url);
	$ua->proxy(http => $constants->{http_proxy}) if $constants->{http_proxy};
	my $timeout = 30;
	$timeout = $options->{timeout} if $options->{timeout};
	$ua->timeout($timeout);
	my $result = $ua->request($request);

	if ($result->is_success) {
		return $result->content;
	} else {
		return "";
	}
}


################################################################################

sub getTop10Comments {
	my $A =	$backupdb->getTop10Comments();

	my $reasons = $slashdb->getReasons();

	my $block;
	foreach (@$A) {
		my($sid, $title, $cid, $subj, $d, $nickname, $points, $reason) = @$_;
		$block .= <<EOT;

&middot; <B><A HREF="$constants->{rootdir}/comments.pl?sid=$sid&amp;cid=$cid">$subj</A>
	($points points, $reasons->{$reason}{name})
	by $nickname</B>
	on $d
	<FONT SIZE="1">attached to
	<A HREF="$constants->{rootdir}/article.pl?sid=$sid">$title</A></FONT><BR>
EOT
	}
	setblock("top10comments", $block);

}

#################################################################
sub getWhatsPlayingBox {
	my $list = $slashdb->getWhatsPlaying();

	my $block = '';
	foreach (@$list) {
		$block .= "<B>$_->[0]:</B><UL>";
		my @items = split /|/, $_->[1];
		foreach (@items) {
			$block .= "<LI>$_</LI>";
		}
		$block .= "</UL><BR>";
	}

	setblock("whatsplaying", $block);
}

#################################################################
sub getSlashdotPoll {
	setblock("poll", pollbooth('_currentqid', 1));
}


#################################################################
sub portaldLog {
	doLog('portald', \@_);
}

=head2 Fortune

The fortune command.

=cut


#################################################################
sub getUptime {
	my $x = `/usr/bin/uptime`;
	$x = "<B>time:</B> $x";
	$x =~ s/up/\n<BR><B>uptime:<\/B>/g;
	$x =~ s/load average:/\n<BR><B>load average:<\/B>/;
	my $ps = `/bin/ps aux | /usr/bin/wc -l`;
	$ps--;
	$x .= "<BR><B>processes:</B> $ps <BR>";

	my $stats = $x;

#	my $tc = $constants->{totalComments};
	my $th = $constants->{totalhits};

#	$stats .= "<B>yesterday:</B> $yesterday<BR>
#		<B>today:</B> $today<BR>
#		<B>ever:</B> $th<BR>";
	$stats .= "<B>totalhits:</B> $th<BR>";

	setblock('uptime', $stats);
}


#################################################################
sub setblock {
	my($bid, $block) = @_;
	$savedBlocks{$bid} = $block;
	portaldLog("updated $bid");
}


#################################################################
sub getRDF {
	#gets an RDF file, and formats it as a /. block

	my($bid, $url, $other) = @_;
	my $rss = new XML::RSS;
	my $template_name ||= $slashdb->getBlock($bid, 'rss_template');
	my $items ||= $slashdb->getBlock($bid, 'items');
	$items ||= $constants->{max_items};
	$template_name ||= $constants->{default_rss_template};

	my $d = geturl($url);

	if (!$d) {
		portaldLog("failed to get $bid: $@");
		return;
	}
	$d =~ s/&(?!#?[a-zA-Z0-9]+;)/&amp;/g;

	eval { $rss->parse($d) };

	if ($@) {
		my $err = $@;
		$err =~ s/\n/ /;
		portaldLog("$bid did not parse properly:$err");
		return;
	} else {
		my $str;
		my $i = 0;
		my $bd_regex = qr{\Q$constants->{basedomain}};
		for my $item (@{$rss->{items}}) {
			# hopefully this xmldecode() will break nothing
			for (keys %{$item}) {
				$item->{$_} = xmldecode($item->{$_});
			}

			# filter URL like the rest
			my $link = fudgeurl($item->{link});
			next unless $link;

			# convert links to sections of our own site
			# into a non-scheme-specific format
			my $uri = URI->new($link);
			if ($uri->can('authority')) {
				$uri->scheme(undef) if $uri->authority =~ /\b$bd_regex\Z/;
			}

			$item->{link} = $uri->as_string or next;

			# this is if you want a nice numbering 
			# layout in your block <ol> works, but 
			# is ugly
			$item->{counter} = $i + 1;
			$str .= slashDisplay($template_name, {
				item	=> $item,
			}, {
				Nocomm	=> 1,
				Return	=> 1,
				Page	=> 'portald'
			});
			$slashdb->createRSS($bid, $item)
				if $constants->{rss_store};

			last if ++$i >= $items && $items > 0;
		}

		setblock($bid, "$str$other");
		return 1;
	}
}

#################################################################
# wow, now it's time to actually do something
doLogInit('portald');

portaldLog("Launching Portald");

$|++;

my $p_site = catfile($constants->{datadir}, 'sbin', 'portald-site');
if (-e $p_site) {
	portaldLog("requiring $p_site");
	require $p_site;
}

portaldLog("Updating Portal Box Thingees");

# loop through all the RDF sites
my $RDFlist = $backupdb->getSitesRDF();

for (@{$RDFlist}) {
	my($bid, $url, $rdf) = ($_->[0], $_->[1], $_->[2]);
	getRDF($bid, $rdf);
}

getTop10Comments();
getWhatsPlayingBox() if $constants->{slashbox_whatsplaying};
getSlashdotPoll();
getUptime();

my $randbid = $slashdb->getBlock('rand','bid');
if ($randbid) {
    setblock("rand", $slashdb->randomBlock()); # NOT backupdb!
}

foreach (keys %savedBlocks) {
	# NOT backupdb!
	$slashdb->setBlock($_, { block => $savedBlocks{$_} });
}

# from 'portald-site'
newSemiRandomBlock($slashdb) if defined &newSemiRandomBlock;

portaldLog("Sucessfully Saved Portals");

# Clean up
doLogExit('portald');

__END__
